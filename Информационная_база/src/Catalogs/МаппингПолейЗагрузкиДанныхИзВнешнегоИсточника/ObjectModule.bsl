
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Перем ПолныйПутьОбъектЗагрузки;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ДанныеЗаполнения.Свойство("ОбъектЗагрузки", ОбъектЗагрузки);
		ДанныеЗаполнения.Свойство("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
		ДанныеЗаполнения.Свойство("ПолныйПутьОбъектЗагрузки", ПолныйПутьОбъектЗагрузки);
		ДанныеЗаполнения.Свойство("Пользователь", Пользователь);
		
		ПолныйПутьОбъектЗагрузки = ?(ПолныйПутьОбъектЗагрузки = Неопределено, "", ПолныйПутьОбъектЗагрузки);
		
		ВыбранныеПоля = Неопределено;
		Если ДанныеЗаполнения.Свойство("ВыбранныеПоля", ВыбранныеПоля) Тогда
			
			Если ТипЗнч(ВыбранныеПоля) = Тип("Соответствие") Тогда
				
				ДоступныеПоляЗагрузкиДанных = Справочники.МаппингПолейЗагрузкиДанныхИзВнешнегоИсточника.ДоступныеПоляЗагрузкиДанныхИзВнешнегоИсточника(ОбъектЗагрузки, ИмяТабличнойЧасти, Истина, ПолныйПутьОбъектЗагрузки);
				Для каждого ЭлементСоответствия Из ВыбранныеПоля Цикл
					
					Если ТипЗнч(ЭлементСоответствия.Ключ) = Тип("Строка") Тогда
						
						ОписаниеПоля = ДоступныеПоляЗагрузкиДанных[ЭлементСоответствия.Ключ];
						
					Иначе
						
						ОписаниеПоля = Строка(ЭлементСоответствия.Ключ);
						
					КонецЕсли;
					
					Если ОписаниеПоля = Неопределено Тогда
						
						Продолжить;
						
					КонецЕсли;
					
					НоваяСтрока = МаппингПолей.Добавить();
					НоваяСтрока.НомерКолонки		= ЭлементСоответствия.Значение;
					НоваяСтрока.ПредставлениеПоля	= ОписаниеПоля;
					
					Если ТипЗнч(ЭлементСоответствия.Ключ) = Тип("Строка") Тогда
						
						НоваяСтрока.ИмяПоля = ЭлементСоответствия.Ключ;
						
					Иначе
						
						НоваяСтрока.ДополнительныйРеквизит = ЭлементСоответствия.Ключ;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли