
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РегионРежимОтбора",   РегионРежимОтбора);
	Параметры.Свойство("РегионПредставление", РегионПредставление);
	Параметры.Свойство("РегионЗначенияПолей", РегионЗначенияПолей);
	
	Если ПустаяСтрока(РегионРежимОтбора) Тогда
		РегионРежимОтбора = 0;
	КонецЕсли;
	
	// Видимость настройки профиля, только при наличии прав.
	Элементы.ГиперссылкаПрофильОбмена.Видимость = ТорговыеПредложения.ПравоНастройкиТорговыхПредложений();
	
	УстановитьВидимостьДоступность(РегионРежимОтбора, Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимВыбораПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(РегионРежимОтбора, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПрофильОбменаНажатие(Элемент)
	
	ПараметрыОткрытия = Новый Структура("РежимОткрытия", "РегионыЗакупки");
	ОчиститьСообщения();
	ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ПрофильАбонента", ПараметрыОткрытия,
		ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РегионПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуКонтактнойИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	Если РегионРежимОтбора = 2 И ПустаяСтрока(РегионПредставление) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран регион'"),, "РегионПредставление");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("РегионРежимОтбора", РегионРежимОтбора);
	ПараметрыЗакрытия.Вставить("РегионПредставление", ?(РегионРежимОтбора = 2, РегионПредставление, ""));
	ПараметрыЗакрытия.Вставить("РегионЗначенияПолей", ?(РегионРежимОтбора = 2, РегионЗначенияПолей, ""));
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(РежимВыбора, Элементы)

	Элементы.ГиперссылкаПрофильОбмена.Доступность = РежимВыбора = 1;
	Элементы.ГруппаВводаРегиона.Доступность = РежимВыбора = 2;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформации()
	
	ПараметрыВида = ПараметрыВидаКонтактнойИнформации();
	ПараметрыВида.Наименование = НСтр("ru = 'Введите регион поставщиков'");
	ПараметрыВида.НастройкиПроверки.ВключатьСтрануВПредставление = Истина;
	ПараметрыВида.НастройкиПроверки.ПроверятьПоФИАС              = Истина;
	ПараметрыВида.РедактированиеТолькоВДиалоге                   = Истина;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВида, РегионЗначенияПолей, РегионПредставление);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВидаКонтактнойИнформации()
	
	Возврат УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
		ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		РегионПредставление = Результат.Представление;
		РегионЗначенияПолей = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти